/*
 * CDDL HEADER START
 *
 * The contents of this file are subject to the terms of the
 * Common Development and Distribution License, Version 1.0 only
 * (the "License").  You may not use this file except in compliance
 * with the License.
 *
 * You can obtain a copy of the license at usr/src/OPENSOLARIS.LICENSE
 * or http://www.opensolaris.org/os/licensing.
 * See the License for the specific language governing permissions
 * and limitations under the License.
 *
 * When distributing Covered Code, include this CDDL HEADER in each
 * file and include the License file at usr/src/OPENSOLARIS.LICENSE.
 * If applicable, add the following below this CDDL HEADER, with the
 * fields enclosed by brackets "[]" replaced with your own identifying
 * information: Portions Copyright [yyyy] [name of copyright owner]
 *
 * CDDL HEADER END
 */
/*
 * Copyright 2004 Sun Microsystems, Inc.  All rights reserved.
 * Use is subject to license terms.
 */

#pragma ident	"%Z%%M%	%I%	%E% SMI"

#pragma dictionary "SUN4U"

#define	AGENT_ID_MASK	0x1f
#define	AGENT_ID_SHIFT	24

#define	HB_FIT		1000
#define HBUS_FIT	1000
#define PCI_BUS_FIT	500
#define PCI_DEV_FIT	1000
#define	CPU_FIT		500

#define	PCI_HB_DEV_PATH hostbridge/pcibus/pcidev[32]/pcifn[0]

fru hostbridge;
asru hostbridge;

event fault.io.schizo@hostbridge,
    FITrate=HB_FIT, FRU=hostbridge, ASRU=hostbridge;

event error.io.sch.saf.dstat@hostbridge/pcibus/pcidev/pcifn;
event error.io.sch.saf.to@hostbridge/pcibus/pcidev/pcifn;
event error.io.sch.saf.bus@hostbridge/pcibus/pcidev/pcifn;
event error.io.sch.saf.tx-ta@hostbridge/pcibus/pcidev/pcifn;

event ereport.io.sch.saf.to@hostbridge{within(5s)};
event ereport.io.sch.saf.bus@hostbridge{within(5s)};
event ereport.io.pci.sta@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.pci.rta@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.pci.target-rta@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.sch.saf.bca@hostbridge{within(5s)};
event ereport.io.sch.saf.bcb@hostbridge{within(5s)};
event ereport.io.sch.saf.ciq-to@hostbridge{within(5s)};
event ereport.io.sch.saf.lpq-to@hostbridge{within(5s)};
event ereport.io.sch.saf.sfpq-to@hostbridge{within(5s)};
event ereport.io.sch.saf.ufpq-to@hostbridge{within(5s)};
event ereport.io.sch.saf.ape@hostbridge{within(5s)};
event ereport.io.sch.ecc.pce@hostbridge{within(5s)};
event ereport.io.sch.ecc.pue@hostbridge{within(5s)};
event ereport.io.sch.ecc.s-pce@hostbridge{within(5s)};
event ereport.io.sch.ecc.s-pue@hostbridge{within(5s)};
event ereport.io.sch.ecc.thresh@hostbridge{within(5s)};
event ereport.io.sch.saf.dstat@hostbridge{within(5s)};

/*
 * A faulty Schizo hostbridge may cause:
 *
 *  - bca:	bad safari command from PCI block A.
 *  - bcb:	bad safari command from PCI block B.
 *  - ciq-to:	coherent input queue timeout.
 *  - lpq-to:	local PIO queue timeout.
 *  - sfpq-to:	safari foreign PIO queue timeout.
 *  - ufpq-to:	UPA foreign PIO queue timeout.
 *  - ape:	address parity error.
 *  - pue:	PIO uncorrectable error, bad reader.
 *  - s-pue:	secondary PIO UE, bad reader.
 *  - ecc:	multiple PIO CEs.
 *  - to:	safari bus timeout.
 *  - bus:	safari bus error.
 *  - dstat:	errant dstat on incoming data.
 */
prop fault.io.schizo@hostbridge (0)->
    ereport.io.sch.saf.bca@hostbridge,
    ereport.io.sch.saf.bcb@hostbridge,
    ereport.io.sch.saf.ciq-to@hostbridge,
    ereport.io.sch.saf.lpq-to@hostbridge,
    ereport.io.sch.saf.sfpq-to@hostbridge,
    ereport.io.sch.saf.ufpq-to@hostbridge,
    ereport.io.sch.saf.ape@hostbridge,
    ereport.io.sch.ecc.pue@hostbridge,
    ereport.io.sch.ecc.s-pue@hostbridge,
    ereport.io.sch.ecc.thresh@hostbridge,
    error.io.sch.saf.to@hostbridge/pcibus/pcidev/pcifn,
    error.io.sch.saf.bus@hostbridge/pcibus/pcidev/pcifn,
    error.io.sch.saf.dstat@hostbridge/pcibus/pcidev/pcifn;

prop error.io.sch.saf.to@hostbridge/pcibus/pcidev/pcifn (2)->
    ereport.io.sch.saf.to@hostbridge,
    error.io.sch.saf.tx-ta@hostbridge/pcibus/pcidev/pcifn;

prop error.io.sch.saf.tx-ta@hostbridge/pcibus/pcidev/pcifn (1)->
    ereport.io.pci.sta@PCI_HB_DEV_PATH,
    ereport.io.pci.target-rta@PCI_HB_DEV_PATH;

prop error.io.sch.saf.tx-ta@hostbridge/pcibus/pcidev/pcifn (0)->
    ereport.io.pci.rta@hostbridge/pcibus/pcidev/pcifn;

prop error.io.sch.saf.bus@hostbridge/pcibus/pcidev/pcifn (2)->
    ereport.io.sch.saf.bus@hostbridge,
    error.io.sch.saf.tx-ta@hostbridge/pcibus/pcidev/pcifn;

prop error.io.sch.saf.dstat@hostbridge/pcibus/pcidev/pcifn (1)->
    ereport.io.sch.saf.dstat@hostbridge;

prop error.io.sch.saf.dstat@hostbridge/pcibus/pcidev/pcifn (0)->
    error.io.sch.saf.tx-ta@hostbridge/pcibus/pcidev/pcifn;

engine serd.io.schizo.ecc@hostbridge,
    N=3, T=1day, method=persistent,
    trip=ereport.io.sch.ecc.thresh@hostbridge;

event upset.io.schizo@hostbridge,
    engine=serd.io.schizo.ecc@hostbridge;

/*
 * An upset schizo may cause:
 *
 *  - pce:	PIO correctable error.
 */
prop upset.io.schizo@hostbridge (0)->
    ereport.io.sch.ecc.pce@hostbridge;

fru cpu;

event fault.io.datapath@cpu, FITrate=CPU_FIT, FRU=cpu;

event ereport.io.sch.saf.ssm-dis@hostbridge{within(5s)};
event ereport.io.sch.saf.cpu0-par@hostbridge{within(5s)};
event ereport.io.sch.saf.cpu0-bidi@hostbridge{within(5s)};
event ereport.io.sch.saf.cpu1-par@hostbridge{within(5s)};
event ereport.io.sch.saf.cpu1-bidi@hostbridge{within(5s)};
event ereport.io.cpu.ecc.thresh@cpu;

/*
 * A faulty xcal CPU[0] may cause:
 *
 *  - cpu0-par:		parity error on the unidirectional signals.
 *  - cpu0-bidi:	parity error on the bi-directional signals.
 */
prop fault.io.datapath@cpu[0] (0)->
    ereport.io.sch.saf.cpu0-par@hostbridge,
    ereport.io.sch.saf.cpu0-bidi@hostbridge;

/*
 * A faulty xcal CPU[1] may cause:
 *
 *  - cpu1-par:		parity error on the unidirectional signals.
 *  - cpu1-bidi:	parity error on the bidirectional signals.
 */
prop fault.io.datapath@cpu[1] (0)->
    ereport.io.sch.saf.cpu1-par@hostbridge,
    ereport.io.sch.saf.cpu1-bidi@hostbridge;

/*
 * A faulty CPU may cause:
 *
 *  - to:	safari bus timeout.
 *  - bus:	safari bus error.
 *  - dstat:	incorrect dstat sent to hostbridge.
 *  - ssm-dis:	ssm command sent to hostbridge when not enabled.
 *  - ape:	safari address parity error.
 *  - pue:	PIO uncorrectable error.
 *  - ecc:	multiple PIO CEs.
 */
prop fault.io.datapath@cpu (0)->
    error.io.sch.saf.to@hostbridge/pcibus/pcidev/pcifn,
    error.io.sch.saf.bus@hostbridge/pcibus/pcidev/pcifn,
    error.io.sch.saf.dstat@hostbridge/pcibus/pcidev/pcifn,
    ereport.io.sch.saf.ssm-dis@hostbridge,
    ereport.io.sch.saf.ape@hostbridge;

prop fault.io.datapath@cpu[cpuid] (0)->
    ereport.io.sch.ecc.pue@hostbridge
    {((payloadprop("ecc-afsr") >> AGENT_ID_SHIFT) & AGENT_ID_MASK) == cpuid},
    ereport.io.cpu.ecc.thresh@cpu[cpuid];

engine serd.io.cpu.ecc@cpu,
    N=3, T=1day, method=persistent,
    trip=ereport.io.cpu.ecc.thresh@cpu;

event upset.io.datapath@cpu,
    engine=serd.io.cpu.ecc@cpu;

/*
 * An upset CPU may cause:
 *
 *  - pce:	PIO correctable error.
 */
prop upset.io.datapath@cpu[cpuid] (0)->
    ereport.io.sch.ecc.pce@hostbridge
    {((payloadprop("ecc-afsr") >> AGENT_ID_SHIFT) & AGENT_ID_MASK) == cpuid};

event fault.io.hbus@hostbridge,
    FITrate=HBUS_FIT, FRU=hostbridge, ASRU=hostbridge;

event ereport.io.hbus.ecc.thresh@hostbridge;

/*
 * A faulty host bus may cause:
 *
 *  - ape:		address parity error.
 *  - cpu0-par:		parity error on the unidirectional signals.
 *  - cpu0-bidi:	parity error on the bidirectional signals.
 *  - cpu1-par:		parity error on the unidirectional signals.
 *  - cpu1-bidi:	parity error on the bidirectional signals.
 *  - pue:		PIO uncorrectable error.
 *  - s-pue:		secondary PIO UE.
 *  - ecc:		multiple PIO CEs.
 */
prop fault.io.hbus@hostbridge (0)->
    ereport.io.sch.saf.ape@hostbridge,
    ereport.io.sch.saf.cpu0-par@hostbridge,
    ereport.io.sch.saf.cpu0-bidi@hostbridge,
    ereport.io.sch.saf.cpu1-par@hostbridge,
    ereport.io.sch.saf.cpu1-bidi@hostbridge,
    ereport.io.sch.ecc.pue@hostbridge,
    ereport.io.sch.ecc.s-pue@hostbridge,
    ereport.io.hbus.ecc.thresh@hostbridge;

engine serd.io.hbus.ecc@hostbridge,
    N=3, T=1day, method=persistent,
    trip=ereport.io.hbus.ecc.thresh@hostbridge;

event upset.io.hbus@hostbridge,
    engine=serd.io.hbus.ecc@hostbridge;

/*
 * An upset host bus may cause:
 *
 *  - pce:	PIO correctable error.
 */
prop upset.io.hbus@hostbridge (0)->
    ereport.io.sch.ecc.pce@hostbridge;

event defect.io.pci.driver@hostbridge/pcibus/pcidev/pcifn;

event error.io.sch.mmu@hostbridge/pcibus/pcidev/pcifn;
event error.io.sch.saf.unmapped@hostbridge/pcibus/pcidev/pcifn;

event ereport.io.sch.mmu@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.sch.saf.um@hostbridge{within(5s)};

/*
 * A defective PCI driver may cause:
 *
 *  - unmapped:	safari unmapped address error.
 *  - mmu:	a iommu translation error.
 */
prop defect.io.pci.driver@hostbridge/pcibus/pcidev/pcifn (0)->
    error.io.sch.saf.unmapped@hostbridge/pcibus/pcidev/pcifn,
    error.io.sch.mmu@hostbridge/pcibus/pcidev/pcifn;

prop error.io.sch.saf.unmapped@hostbridge/pcibus/pcidev/pcifn (1)->
    ereport.io.sch.saf.um@hostbridge;

prop error.io.sch.saf.unmapped@hostbridge/pcibus/pcidev/pcifn (0)->
    error.io.sch.saf.tx-ta@hostbridge/pcibus/pcidev/pcifn;

prop error.io.sch.mmu@hostbridge/pcibus/pcidev/pcifn (2)->
    ereport.io.sch.mmu@PCI_HB_DEV_PATH,
    error.io.sch.saf.tx-ta@hostbridge/pcibus/pcidev/pcifn;

event defect.io.pci.driver@hostbridge;

/*
 * A defective PCI nexus driver may cause:
 *
 *  - unmapped:	safari unmapped address error.
 */
prop defect.io.pci.driver@hostbridge (0)->
    error.io.sch.saf.unmapped@hostbridge/pcibus/pcidev/pcifn;

fru pcibus;
asru pcibus;

event fault.io.pci.bus@hostbridge/pcibus,
    FITrate=PCI_BUS_FIT, FRU=pcibus, ASRU=pcibus;

event ereport.io.sch.bu@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.sch.s-bu@hostbridge/pcibus/pcidev/pcifn{within(5s)};

/*
 * A faulty PCI bus may cause:
 *
 *  - bu:	PCI bus unusable error.
 *  - s-bu:	secondary PCI bus unusable error.
 */
prop fault.io.pci.bus@hostbridge/pcibus (0)->
    ereport.io.sch.bu@PCI_HB_DEV_PATH,
    ereport.io.sch.s-bu@PCI_HB_DEV_PATH;

fru pcibus/pcidev;
asru pcibus/pcidev/pcifn;

event fault.io.pci.device@hostbridge/pcibus/pcidev/pcifn,
    FITrate=PCI_DEV_FIT, FRU=pcibus/pcidev, ASRU=pcibus/pcidev/pcifn;

event error.io.sch.pbm.rl@hostbridge/pcibus/pcidev/pcifn;
event error.io.sch.pbm.tto@hostbridge/pcibus/pcidev/pcifn;
event error.sch.cpu.berr@cpu;
event error.io.pci.rec-ma@hostbridge/pcibus/pcidev/pcifn;
event error.io.pci.rec-mdpe@hostbridge/pcibus/pcidev/pcifn;
event error.io.pci.rec-ta@hostbridge/pcibus/pcidev/pcifn;
event error.io.pci.serr@hostbridge/pcibus;

event ereport.io.sch.sbh@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.sch.pbm.rl@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.sch.pbm.tto@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.sch.pbm.s-rl@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.sch.pbm.s-tto@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.sch.pbm.s-ma@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.sch.pbm.s-rta@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.sch.pbm.s-mdpe@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.sch.pbm.target-rl@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.sch.pbm.target-tto@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.pci.rserr@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.pci.sserr@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.cpu.ultraSPARC-III.berr@cpu{within(5s)};
event ereport.cpu.ultraSPARC-IIIplus.berr@cpu{within(5s)};
event ereport.cpu.ultraSPARC-IV.berr@cpu{within(5s)};
event ereport.cpu.ultraSPARC-IVplus.berr@cpu{within(5s)};

/*
 * A faulty PCI device may cause:
 *
 *  - sbh:	a streaming byte hole error.
 *  - rl:	it to exceed the number retriesfor a transaction.
 *  - tto:	it to not assert trdy# within the alloted timeout.
 */
prop fault.io.pci.device@hostbridge/pcibus/pcidev/pcifn (0)->
    ereport.io.sch.sbh@PCI_HB_DEV_PATH,
    error.io.sch.pbm.rl@hostbridge/pcibus/pcidev/pcifn,
    error.io.sch.pbm.tto@hostbridge/pcibus/pcidev/pcifn;

prop error.io.sch.pbm.rl@hostbridge/pcibus/pcidev/pcifn (1)->
    ereport.io.sch.pbm.rl@PCI_HB_DEV_PATH,
    ereport.io.sch.pbm.s-rl@PCI_HB_DEV_PATH;

prop error.io.sch.pbm.rl@hostbridge/pcibus/pcidev/pcifn (0)->
    ereport.io.sch.pbm.target-rl@hostbridge/pcibus/pcidev/pcifn;

prop error.io.sch.pbm.rl@hostbridge/pcibus/pcidev/pcifn (0)->
    error.sch.cpu.berr@cpu;

prop error.io.sch.pbm.tto@hostbridge/pcibus/pcidev/pcifn (1)->
    ereport.io.sch.pbm.tto@PCI_HB_DEV_PATH,
    ereport.io.sch.pbm.s-tto@PCI_HB_DEV_PATH;

prop error.io.sch.pbm.tto@hostbridge/pcibus/pcidev/pcifn (0)->
    ereport.io.sch.pbm.target-tto@hostbridge/pcibus/pcidev/pcifn;

prop error.io.sch.pbm.tto@hostbridge/pcibus/pcidev/pcifn (1)->
    ereport.io.sch.bu@PCI_HB_DEV_PATH;

/*
 * Need to add the following schizo specific propagations to complete the PCI
 * fault tree.
 */
prop error.io.pci.rec-ma@PCI_HB_DEV_PATH (0)->
    ereport.io.sch.pbm.s-ma@PCI_HB_DEV_PATH;

prop error.io.pci.rec-ta@PCI_HB_DEV_PATH (0)->
    ereport.io.sch.pbm.s-rta@PCI_HB_DEV_PATH;

prop error.io.pci.rec-mdpe@PCI_HB_DEV_PATH (0)->
    ereport.io.sch.pbm.s-mdpe@PCI_HB_DEV_PATH;

prop error.io.pci.rec-ta@PCI_HB_DEV_PATH (0)->
    error.sch.cpu.berr@cpu;

prop error.io.pci.rec-mdpe@PCI_HB_DEV_PATH (0)->
    error.sch.cpu.berr@cpu;

prop error.io.pci.rec-ma@PCI_HB_DEV_PATH (0)->
    error.sch.cpu.berr@cpu;

prop error.io.pci.serr@hostbridge/pcibus (1)->
    ereport.io.pci.rserr@PCI_HB_DEV_PATH;

prop error.io.pci.serr@hostbridge/pcibus (0)->
    ereport.io.pci.sserr@PCI_HB_DEV_PATH;

prop error.sch.cpu.berr@cpu (1)->
    ereport.cpu.ultraSPARC-III.berr@cpu,
    ereport.cpu.ultraSPARC-IIIplus.berr@cpu,
    ereport.cpu.ultraSPARC-IV.berr@cpu,
    ereport.cpu.ultraSPARC-IVplus.berr@cpu;

event ereport.io.sch.nodiag@hostbridge;

/*
 * Upset used to hide ereports that can not be currently diagnosed.
 */
engine serd.io.sch.nodiag@hostbridge,
    N=1000, T=1hour, method=persistent,
    trip=ereport.io.sch.nodiag@hostbridge;

event upset.io.sch.nodiag@hostbridge,
    engine=serd.io.sch.nodiag@hostbridge;

prop upset.io.sch.nodiag@hostbridge (0)->
    ereport.io.sch.ecc.s-pce@hostbridge,
    ereport.io.sch.nodiag@hostbridge;
